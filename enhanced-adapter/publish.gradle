apply plugin: 'maven-publish'
apply plugin: 'signing'

def isReleaseBuild() {
    return version.contains("SNAPSHOT") == false
}

static def getReleaseRepositoryUrl() {
    return "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
}

static def getSnapshotRepositoryUrl() {
    return "https://oss.sonatype.org/content/repositories/snapshots/"
}

static def getRepositoryUsername() {
    return System.getenv("SONATYPE_NEXUS_USERNAME")
}

static def getRepositoryPassword() {
    return System.getenv("SONATYPE_NEXUS_PASSWORD")
}

afterEvaluate { project ->
    publishing {
        repositories {
            maven {
                url = isReleaseBuild() ? releaseRepositoryUrl : snapshotRepositoryUrl
                credentials {
                    username = repositoryUsername
                    password = repositoryPassword
                }
            }
        }
        publications {
            release(MavenPublication) {
                from components.release
                groupId = project.groupId
                artifactId = project.artifactId
                version = project.version
                artifact(androidSourcesJar)

                pom {
                    name.set('Enhanced Adapter')
                    setPackaging('aar')
                    description.set('A RecyclerView Adapter extension to make life easier.')
                    url.set('https://github.com/MrBean355/enhanced-adapter')
                    licenses {
                        license {
                            name.set('The Apache License, Version 2.0')
                            url.set('http://www.apache.org/licenses/LICENSE-2.0.txt')
                            distribution.set('repo')
                        }
                    }
                    scm {
                        url.set('https://github.com/MrBean355/enhanced-adapter')
                        connection.set('scm:git:git://github.com/MrBean355/enhanced-adapter.git')
                        developerConnection.set('scm:git:ssh://github.com/MrBean355/enhanced-adapter.git')
                    }
                    developers {
                        developer {
                            id.set('MrBean355')
                            name.set('Michael Johnston')
                        }
                    }
                }
            }
        }
    }

    signing {
        sign(publishing.publications.release)
        setRequired(isReleaseBuild())
    }
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.source
}